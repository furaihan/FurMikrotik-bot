:local send [:parse [/system script get tg_sendMessage source]];
:local tolower [:parse [/system script get func_lowercase source]];
:local newline [:parse [/system script get func_newline source]];
:local param1 [:pick $params 0 [:find $params " "]];
:local param2 [:pick $params ([:find $params " "]+1) [:len $params]];

:local paramsLower [$tolower $params];
#check user file
:local path ("tg-data/catatan/user-$fromid-data.txt")
:if ([:len [/file find where name=$path]] < 1) do={
    /system identity print file=$path
    :delay 1500ms;
    /file set $path contents=("\"asmanipun\"=\"$from\"")
    $send chat=$chatid text=("New user found, we are creating a file configuration for you, then you can try again in 10 seconds")
    :return 1
}
:local sendHelp do={
    :local send [:parse [/system script get tg_sendMessage source]];
    :local txt ("Hi $from, you can use this command to save your note (max 1400 character)%0A%0A\
                *USAGE:*%0A/$cmd <title> <note>%0A%0A*EXAMPLE:*%0A/$cmd ubuntu-link https://mirror.labkom.id/ubuntu-releases/%0A%0A\
                *NOTE:*%0A> Title must be in 1 word (without space)%0A> There's a bug when you use a comma (,) in your note, so avoid using it until its get fixed")
    $send chat=$chatid text=$txt

}

:if (($paramsLower = "help") or ([:len $params] = 0)) do={$sendHelp from=$from chatid=$chatid cmd=$cmd; :return -1}

:local userData [:parse (":local data {".[/file get $path contents]."}; :return \$data")]
:set userData [$userData]
:local catatanUser [/file get $path contents]
#check if the title of the note is available
:local isNameAvailable (([:typeof ($userData->"$param1")] = "nothing") or ([:typeof ($userData->"$param1")] = "nil"))
:if (!$isNameAvailable) do={$send chat=$chatid text=("Sorry, $param1 note has been used, please try the other title");:return 1}
:if ([:typeof [:find $params " "]] != "nil") do={
    :local isi [$newline $param2]
    :if ([:len $param2] > 1500) do={
        $send chat=$chatid text=("error: text is too long")
        :return 1
    }
    :set catatanUser ($catatanUser."; \"$param1\"=\"$isi\"")
    /file set $path contents=$catatanUser
    $send chat=$chatid text=("*$param1*%0AThis note has been added successfully") mode=Markdown
}