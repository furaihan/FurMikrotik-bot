:local send [:parse [/system script get tg_sendMessage source]]
:local gkey [:parse [/system script get tg_getkey source]]
:local tolower [:parse [/system script get func_lowercase source]]
:local param1 [:pick $params 0 [:find $params " "]]
:local param2 [:pick $params ([:find $params " "]+1) [:len $params]]
:local param3 [:pick [:pick $params ([:find $params " "]+1) [:len $params]] ([:find [:pick $params ([:find $params " "]+1) [:len $params]] " "]+1) [:len [:pick $params ([:find $params " "]+1) [:len $params]]]]
:if ([:len [:find $param2 " "]]>0) do={
	:set param2 [:pick [:pick $params ([:find $params " "]+1) [:len $params]] 0 [:find [:pick $params ([:find $params " "]+1) [:len $params]] " "]]
} else={
	:set param3 ""
}


:local urlEncode do={
  :local Input [ :tostr $1 ];
  :local Return "";

  :if ([ :len $Input ] > 0) do={
    :local Chars " !\"#\$&'()*+,:;<=>\?@[\\]^`{|}~";
    :local Subs { "%20"; "%21"; "%22"; "%23"; "%24"; "%26"; "%27"; "%28"; "%29";
                  "%2A"; "%2B"; "%2C"; "%3A"; "%3B"; "%3C"; "%3D"; "%3E"; "%3F"; "%40";
                  "%5B"; "%5C"; "%5D"; "%5E"; "%60"; "%7B"; "%7C"; "%7D"; "%7E" };

    :for I from=0 to=([ :len $Input ] - 1) do={
      :local Char [ :pick $Input $I ];
      :local Replace [ :find $Chars $Char ];

      :if ([ :len $Replace ] > 0) do={
        :set Char ($Subs->$Replace);
      }
      :set Return ($Return . $Char);
    }
  }
	:return $Return;
}
:local paramsLower [$tolower $params];

:local sendHelp do={
	:local send [:parse [/system script get tg_sendMessage source]]
	:local helpText ("Find a geolocation of an IP address including region, city, and country%0A%0A")
	:set helpText ($helpText."*Usage*:%0A")
	:set helpText ($helpText."/lookup _<domain>_%0A")
	:set helpText ($helpText."/lookup _<ip-address>_%0A%0A")
	:set helpText ($helpText."*Example*:%0A")
	:set helpText ($helpText."/lookup _google.com.sg_%0A")
	:set helpText ($helpText."/lookup _1.1.1.1_%0A%0A")
	:put $helpText
	$send chat=$chatid text=$helpText mode="Markdown"
}

:if ([:len $params] > 0) do={
	:if ($paramsLower = "help") do={$sendHelp;}
	:if ((([:typeof [:toip $params]] = "ip") or ([:typeof [:resolve $paramsLower]] = "ip")) and ([:len $param1] = 1) ) do={
		:local urlLookup ("http://ip-api.com/json/$paramsLower")
		:put $urlLookup
		:local lookupData ([/tool fetch mode=http url=$urlLookup output=user as-value]->"data")
		:put $lookupData
		:local status [$gkey key=status text=$lookupData]
		:put $status
		:if ($status = "success") do={
			:local sendThisText (" <b>Lookup $paramsLower</b> %0A%0A");
			:local queryIP [:pick $lookupData ([:find $lookupData "\"query\""] + [:len "\"query\""]+ 2 ) ([:len $lookupData ] - 2)];
			:set sendThisText ($sendThisText."<b>Query</b>: $queryIP%0A");
			:local asNumber [$gkey key=as text=$lookupData];
			:set sendThisText ($sendThisText."<b>AS Number</b>: $asNumber%0A");
			:local isp [$gkey key=isp text=$lookupData];
			:if ([:len $isp] > 0) do={:set sendThisText ($sendThisText."<b>Internet Provider</b>: $isp%0A")} else={:set sendThisText ($sendThisText."<b>Internet Provider</b>: Unknown%0A")}
			:local country [$gkey key=country text=$lookupData];
			:local regionName [$gkey key=regionName text=$lookupData];
			:local city [$gkey key=city text=$lookupData];
			:if ([:len $country] > 0) do={:set sendThisText ($sendThisText."<b>Country</b>: $country%0A")} else={:set sendThisText ($sendThisText."<b>Country</b>: Unknown%0A")}
			:if ([:len $regionName] > 0) do={:set sendThisText ($sendThisText."<b>Region</b>: $regionName%0A")} else={:set sendThisText ($sendThisText."<b>Region</b>: Unknown%0A")}
			:if ([:len $city] > 0) do={:set sendThisText ($sendThisText."<b>City</b>: $city%0A")} else={:set sendThisText ($sendThisText."<b>City</b>: Unknown%0A")}
			:local org [$gkey key=org text=$lookupData]
			:if ([:len $org] > 0) do={:set sendThisText ($sendThisText."<b>ORG</b>: $org%0A")} else={:set sendThisText ($sendThisText."<b>ORG</b>: Unknown%0A")}
			:local timeZone [$gkey key=timezone text=$lookupData]; :set sendThisText ($sendThisText."<b>Time-Zone</b>: $timeZone%0A");
			:put $sendThisText
			$send chat=$chatid text=[$urlEncode $sendThisText] mode="html"
		} else={
			:local failMessage [$gkey key=message text=$lookupData]
			$send chat=$chatid text=("Something wrong. Message: $failMessage")
		}
	} else={
		:if ($paramsLower != "help") do={
			$send chat=$chatid text=("Unknown parameter, please type */lookup help* for more information") mode="Markdown"
		}
	}
} else={
	$sendHelp
}
